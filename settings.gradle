plugins {
    id 'com.gradle.enterprise' version '3.7.2'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.6'
}

gradleEnterprise {
    server = "https://e.grdev.net"

    buildScan {
        def acceptTOSProp = 'java-ordered-properties.acceptGradleTOS'
        if (settings.hasProperty(acceptTOSProp) && Boolean.valueOf(settings.getProperty(acceptTOSProp))) {
            termsOfServiceUrl = 'https://gradle.com/terms-of-service'
            termsOfServiceAgree = 'yes'
        }
        publishAlways()
        captureTaskInputFiles = true
        obfuscation {
            ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } }
        }

        value 'ENV_TEST', System.getenv('ENV_TEST')
        value 'SYS_TEST', System.getProperty('gradle.cache.local.enabled') as String
    }
}

rootProject.name = 'java-ordered-properties'

buildCache {
    local {
        enabled = true
    }

    remote(HttpBuildCache.class) {
        url = 'https://cache-node-hetzner.grdev.net/cache/'
        allowUntrustedServer = false
        credentials { creds ->
            creds.username = System.getenv('GRADLE_ENTERPRISE_CACHE_USERNAME')
            creds.password = System.getenv('GRADLE_ENTERPRISE_CACHE_PASSWORD')
        }
        enabled = true
        push = System.getenv('TEAMCITY_VERSION') != null
    }
}
